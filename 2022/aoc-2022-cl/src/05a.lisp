(defpackage day-05a
  (:use :cl))
(in-package :day-05a)

(defun parse (input)
  (let ((input-and-instructions (cl-ppcre:split "\\n\\n" input)))
    (values
      (parse-setup (car input-and-instructions))
      (parse-instructions (cadr input-and-instructions)))))

(defun parse-setup (setup-input)
  (loop with lines = (str:lines setup-input)
          ;; The last character of the setup instructions is the total number
          ;; of stacks.
        with number-of-stacks =
          (digit-char-p (uiop:last-char setup-input))
          ;; Initialize the stacks as a vector of vectors.
        with stacks =
          (make-array number-of-stacks
            :adjustable t
            :initial-contents
            (loop repeat number-of-stacks
                  collect (make-array 0 :adjustable t :fill-pointer 0)))
        for line in (butlast lines)
          ;; Loop over the characters in the line, collecting only the actual
          ;; characters and adding it to the correct stack.
        do (loop for i from 0
                 for char across line
                   when (and (oddp i) (not (str:blankp char)))
                 do (vector-push-extend char (elt stacks (floor i 4))))
        finally (return (map 'vector #'nreverse stacks))))

(defun parse-instructions (instructions-input)
  (loop for line in (str:lines instructions-input)
        for (move from to) =
          (mapcar #'parse-integer
              (cl-ppcre:all-matches-as-strings "\\d+" line))
        collect (list :move move :from from :to to)))

(defun apply-instruction (stacks instruction)
  (destructuring-bind (&key move from to) instruction
    (loop repeat move
          for crate = (vector-pop (elt stacks (- from 1)))
          do (vector-push-extend crate (elt stacks (- to 1)))
          finally (return stacks))))

(defun read-top-crates (stacks)
  (format nil "窿祜镳骘篝徙徙蝻篌篝徙塍骘麸瓠泸狒ㄥ祠篝徙ō戾铉翳篝徙氅暴泔祆邈糸铉麸瓠泸狒濠┅ㄤ彐躅箫祧ī眭祠轲戾鲠祯瀛忾钿篝徙塍轭篝蝓泗轱铙疳蝮篝蚝骝镯骈戾轭瘐舣暗豇簪┅祜镳骘轭篝蝓泗轱轭轭篝蝓泗轱铙滹ㄡ痧禊轭篝蝓泗轱篝徙塍轭篝蝓泗轱瞟骈钺祆蝈趱蝾篝徙塍┅蝈徜麸瓠泸狒弩篝徙塍┅